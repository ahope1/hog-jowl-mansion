NEW
 0 REM *   THE ADVENTURE OF     *
20 REM *   HOG JOWL MANSION     *
30 REM *    JON J. WAPLES       *
40 REM *    JULY 14, 1981       *
41 REM *     TYPED IN BY        *
42 REM *    MICHAEL BAACKE      *

44 REM Beeb port by lurkio @ Stardot, 2022

50 DATA "IN A DUMBWAITER.",0,0,2,0,21,0,"IN A LONG HALLWAY.",0,6,3,0,0,0,"IN A WORKSHOP.",0,0,0,2,0,0
60 DATA "AT THE BOTTOM OF A SECRET PASSAGE.",0,0,0,0,0,0,"IN A LABYRINTH OF TUNNELS.",0,9,0,0,0,0
70 DATA "IN A TORTURE CHAMBER",2,0,7,0,0,0,"IN A LABYRINTH OF TUNNELS.",0,11,8,6,0,0,"IN A LABYRINTH OF TUNNELS.",0,12,0,7,0,0
80 DATA "IN A LABYRINTH OF TUNNELS.",5,0,10,0,0,0,"IN A LABYRINTH OF TUNNELS.",0,0,11,9,0,0,"IN A LABYRINTH OF TUNNELS."
90 DATA 7,0,0,10,0,0,"IN A LABYRINTH OF TUNNELS",8,16,0,0,0,0,"AT THE LIGHT AT THE END OF THE TUNNEL.",0,0,14,0,0,0
100 DATA "IN A LABYRINTH OF TUNNELS.  I SEE A LIGHT TO THE WEST.   ",0,0,15,13,0,0,"IN A LABYRINTH OF TUNNELS.",0,0,16,14,0,0
110 DATA "IN A LABYRINTH OF TUNNELS.",12,20,0,15,0,0,"IN A LABYRINTH OF TUNNELS.",0,0,18,0,0,0,"IN A LABYRINTH OF TUNNELS."
120 DATA 0,0,19,17,0,0,"IN A LABYRINTH OF TUNNELS.",0,0,20,18,0,0,"IN A LABYRINTH OF TUNNELS.",16,0,0,19,0,0
130 DATA "IN A DUMBWAITER.",0,25,0,0,43,1,"IN THE TRANSPORTER ROOM OF THE STARSHIP ENTERPRISE.  HEY! WHERE'S MR. SPOCK?"
140 DATA 0,26,0,0,0,0,"IN LORD HOG JOWL'S DEN.",0,27,0,0,0,0,"IN A SECRET PASSAGE.",0,0,0,23,0,0
150 DATA "IN A KITCHEN.",0,29,26,0,0,0,"AT THE WEST END OF A HALLWAY.",22,30,27,25,0,0,"AT THE EAST END OF A HALLWAY."
160 DATA 23,31,28,26,0,0,"IN A BEDROOM.",0,0,0,27,0,0,"IN A PANTRY.",25,0,0,0,0,0
170 DATA "IN A LOBBY.",26,34,0,0,0,0,"IN THE LIVING ROOM.",27,0,0,0,0,0,"IN A WALK-IN CLOSET.",28,0,0,0,0,0,"*ON AN UNCUT LAWN."
180 DATA 0,0,34,0,0,0,"*AT THE FRONT OF HOG JOWL MANSION.",0,36,0,33,0,0,"*ON A NARROW LEDGE",0,0,0,0,0,0
190 DATA "*AT THE GATE TO HOG JOWL MANSION.",0,0,0,0,0,0,"ON A BUS NAMED 'DESIRE'.",0,0,0,0,0,0,"IN A BUS TERMINAL."
200 DATA 0,0,39,0,0,0,"IN A RESTROOM.",0,0,0,38,0,0,"IN A WATER CLOSET.",0,0,0,39,0,0,"IN HOBOKEN, NEW JERSEY.",38,42,38,42,38,42
210 DATA "-- UH OH!  THERE'S A GUY HERE WITH A BIG NOSE AND A SPEECH IMPEDIMENT.  ARRGH!  IT'S CARL SAGAN.",0,0,0,0,0,0
220 DATA "IN A DUMBWAITER.",0,0,44,0,0,21,"IN A ROOM BEARING THE SCENT OF ALCOHOL.",0,0,45,0,0,0,"IN A STORAGE ROOM.",0,46,0,44,0,0
230 DATA "IN A DUSTY ATTIC.",45,0,47,0,0,0,"IN A EVIL SMELLING ROOM.",0,0,0,46,0,0
240 DATA "AT THE TOP OF AN ALUMINUM PSEUDO CHRISTMAS TREE.          ",0,0,0,0,0,31
250 DATA "SIGN SAYS: FLOOR 1","SIG",1,1,"PLAQUE","PLA",1,1,"DUMBWAITER","DUM",1,2,"HAMMER","HAM",2,3,"PLIERS","PLI",2,3,"WRENCH"
260 DATA "WRE",2,3,"ELECTRIC BUZZ SAW","SAW",1,3,"PUSHBUTTON SWITCH","SWI",1,3,"STICK OF DYNAMITE","DYN",2,3
270 DATA "LOCKED METAL DOOR","DOO",1,3,"LOCKED METAL DOOR","DOO",1,4,"SPIRAL STAIRCASE LEADING UP","STA",1,4
280 DATA "CORPSE","COR",2,6,"*RICHARD NIXON WRISTWATCH*","WAT",2,6,"PUDDLES OF BLOOD AND GORE","PUD",1,6
290 DATA "DISEMBODIED LIMBS","LIM",2,6,"*DONALD DUCK NITE LITE*","LIT",2,13,"SIGN SAYS: FLOOR 2","SIG",1,21,"PLAQUE","PLA",1,21
300 DATA "*LAVA LAMP*","LAM",2,23,"BOOKCASE","BOO",1,23,"SECRET PASSAGE","PAS",0,23,"*SECRET DECODER RING*","RIN",0,24
310 DATA "DESK","DES",1,24,"DRAWER","DRA",1,24,"SPIRAL STAIRCASE LEADING DOWN","STA",0,24,"REFRIGERATOR","REF",1,25
320 DATA "BUTCHER BLOCK","BLO",1,25,"*GINSU KNIFE*","KNI",0,25,"DUMB WAITER","DUM",1,25,"PIGGY BANK","BAN",2,28,"WINDOW","WIN",1,35
330 DATA "DIME","DIM",0,28,"LARGE BED","BED",1,28,"*CEREMONIAL BED PAN*","PAN",0,28,"WALK-IN CLOSET","CLO",1,28
340 DATA "*BILLY JOWL (SIC)   RECORD ALBUM*","ALB",2,29,"UMBRELLA","UMB",2,30,"*ALUMINUM PSEUDO CHRISTMAS TREE*","TRE",1,31
350 DATA "BOLTS","BOL",0,31,"WINDOW","WIN",1,31,"*DICE EARRINGS*","EAR",2,32,"SPRINKLER SPRAYING WATER","SPR",1,33
360 DATA "*PLASTIC FLAMINGO*","FLA",0,33,"SIGN SAYS: LOOK SP...","SIG",1,33,"WELCOME MAT","MAT",2,34,"KEY","KEY",0,34
370 DATA "LARGE DOOR","DOO",1,34,"MAILBOX","MAI",1,36,"STICK","STI",2,36,"SIGN SAYS: BEWARE OF DOG","SIG",1,36
380 DATA "MANSION","MAN",1,36,"GUARD DOG","DOG",1,36,"POSTCARD","POS",0,36,"NOTE","NOT",0,36,"PAPER CLIP","CLI",0,36
390 DATA "BUS","BUS",1,36,"BUSDRIVER","DRI",1,37,"GATE","GAT",1,37,"BUS TERMINAL","TER",1,37,"BUS","BUS",1,38,"BILLBOARD","BIL",2,38
400 DATA "LOCKED WATER CLOSET","CLO",1,39,"SINK","SIN",1,39,"LARGE SPIGOT","SPI",1,39,"DAISY SADDLEPAL BB GUN","GUN",2,39
410 DATA "TOILET","TOI",1,40,"SIGN SAYS: LEAVE *TREASURES* HERE THEN SAY: SCORE","SIG",1,40,"SIGN SAYS: FLOOR 3","SIG",1,43
420 DATA "PLAQUE","PLA",1,43,"DISTILLERY","DIS",2,44,"*FOGHORN LEGHORN JELLY GLASSES*","GLA",2,44,"DUMBWAITER","DUM",1,44
430 DATA "55 GALLON DRUMS FILLED WITH ALCOHOL","ALC",1,45,"CLOSED DOOR","DOO",1,45,"DEAD IRS AGENT","AGE",0,45
440 DATA "*GOLD FILLINGS*","FIL",0,45,"*WING TIPPED PATENT LEATHER SHOES*","SHO",0,45,"CHEST","CHE",2,46,"MATCH","MAT",0,46
450 DATA "FALSE PANEL IN FLOOR","FLO",1,46,"ELDERLY COW WITH MORNING BREATH","COW",1,47,"DOOR","DOO",1,47
460 DATA "FALSE PANEL IN CEILING","CEI",1,48,"GO","WAL","RUN","N","S","E","W","U","D","TAK","GET","INV","I","THR","DRO","CLI"
470 DATA "ENT","SHO","TUR","JUM","MOV","OPE","SCO","PUS","PIC","BRE","INS","UNL","UNB","DRI","LIG","PUL","LOO","EXA"
480 DATA "REA","HEL","QUI"

490 MODE7:PRINT:V%=VPOS:B=0:H=0:DIM S% 255

500 DIM A(83),A$(83),B(48),B$(36),C(48),C$(48),D(48),D$(83),E(48),F(48),G(48),H(83)
510 FOR I=1 TO 48: READ C$(I),B(I),C(I),D(I),E(I),F(I),G(I):NEXT
520 FOR I=0 TO 83: READ D$(I),A$(I),H(I),A(I):NEXT
530 FOR I=0 TO 36: READ B$(I): NEXT
540 A=38: C$=CHR$(91) + STRING$(38,CHR$45) + CHR$(93)
550 CLS: PRINT TAB(0,11)"WELCOME TO THE ADVENTURE OF"':FOR I=0TO1:PRINTCHR$141;"HOG JOWL MANSION: VERSION 1.12":NEXT:PRINT'"BY JON J. WAPLES"
560 IF A=42 THEN PRINT "YOU HAVE LOST ALL *TREASURES*.": GOTO 2310
570 GOSUB 2000: D$="": E$="": A$="": PRINTTAB(0,24); : INPUT "--> TELL ME WHAT TO DO? " D$
580 FOR I= 1 TO LEN(D$): IF MID$(D$,I,1)=" " THEN i=I:I=LEN(D$):NEXT:I=i:GOTO 590 ELSE NEXT: GOTO 600
590 E$= RIGHT$(D$,LEN(D$)-I): A$=LEFT$(E$,3)
600 B$=LEFT$(D$,I-1)
610 FOR I=0 TO 36: IF B$(I)=LEFT$(B$,3) THEN i=I:I=36:NEXT:I=i:GOTO 630 ELSE NEXT
620 PROCo("I DON'T KNOW HOW TO "+CHR$(34)+B$+CHR$(34)+" SOMETHING."):PRINT: GOTO 560
630 IF I < 27 ON I+1 GOTO 650,650,650,650,660,670,680,690,700,960,960,1040,1040,1110,1200,690,710,1230,1320,1350,1360,1410,1490,1500,1540,1570,1600
640 ON I-26 GOTO 1630,1640,1660,1680,1730,1750,1750,1870,1930,2300
650 IF A$="NOR" OR B$="N" THEN IF B(A)>0 THEN A=B(A): GOTO 950
660 IF A$="SOU" OR B$="S" THEN IF C(A)>0 THEN A=C(A): GOTO 950
670 IF A$="EAS" OR B$="E" THEN IF D(A)>0 THEN A=D(A): GOTO 950
680 IF A$="WES" OR B$="W" THEN IF E(A)>0 THEN A=E(A): GOTO 950
690 IF A$="UP" OR B$="U" THEN IF F(A)>0 THEN A=F(A): GOTO 950
700 IF A$="DOW" OR B$="D" THEN IF G(A)>0 THEN A=G(A): GOTO 950
710 IF A$<>"DUM" OR A<>2 ELSE IF B=0 THEN A=1: GOTO 950 ELSE PRINT "SOMETHING WON'T FIT.": GOTO 560
720 IF A$<>"DOO" OR A<>3 ELSE IF LEFT$(D$(9),2)="UN" THEN A=4: GOTO 950 ELSE 2220
730 IF A$<>"DOO" OR A<>4 ELSE IF LEFT$(D$(10),2) ="UN" THEN A=3: GOTO 950 ELSE 2220
740 IF A$="STA" AND A=4 THEN A=24: GOTO 950
750 IF A$<>"PAS" OR A<>23 ELSE IF H(21)=1 THEN A=24: GOTO 950 ELSE 2220
760 IF A$<>"STA" OR A<>24 ELSE IF H(25)=1 THEN A=4: GOTO 950 ELSE 2220
770 IF A$<>"DUM" OR A<>25 ELSE IF B=0 THEN A=21: GOTO 950 ELSE PRINT "SOMETHING WON'T FIT.": GOTO 560
780 IF A$="REF" AND A=25 THEN PRINT "GAG, GASP, COUGH, BELCH... I SUFFOCATED!" : GOTO 2280
790 IF A$="CLO" AND A=28 THEN A=32: GOTO 950
800 IF A$="WIN" AND A=31 THEN A=35: GOTO 950
810 IF A$<>"TRE" OR A<>31 ELSE IF H(38)=1 THEN A=48: GOTO 950 ELSE PRINT "I CAN'T. THE TREE'S TOO UNSTABLE.": GOTO 560
820 IF A$="DOO" AND A=34 THEN A=30: GOTO 950
830 IF A$="WIN" AND A=35 THEN A=31: GOTO 950
840 IF A$="BUS" AND (A=36 OR A=38) THEN A=37: GOTO 950
850 IF A$<>"MAN" OR A<>36 ELSE IF LEFT$(D$(52),4)="DEAD" THEN A=34: GOTO 950 ELSE PRINT "THE DOG WON'T LET ME.": GOTO 560
860 IF A$="GAT" AND A=37 THEN A=36: GOTO 950
870 IF A$="TER" AND A=37 THEN A=38: GOTO 950
880 IF A$<>"CLO" OR A<>39 ELSE IF LEFT$(D$(62),2) ="UN" THEN A=40: GOTO 950 ELSE 2220
890 IF A$="TOI" AND A=40 THEN PRINT "AH... I FEEL RELIEVED!": GOTO 560
900 IF A$<>"DUM" OR A<>44 ELSE IF B=0 THEN A=43: GOTO 950 ELSE PRINT "SOMETHING WON'T FIT.": GOTO 560
910 IF A$<>"FLO" OR A<>46 ELSE IF H(38)=1 THEN A=48: GOTO 950 ELSE PROCo("I CAN'T, THERE'S SOMETHING ON THE OTHER SIDE."):PRINT: GOTO 560
920 IF A$="DOO" AND A=47 THEN PRINT "COW WON'T LET ME.": GOTO 560
930 IF A$="CEI" AND A=48 THEN A=46: GOTO 950
940 PRINT "I CAN'T GO IN THAT DIRECTION.": GOTO 560
950 PRINT "OK.": GOTO 560
960 IF A$="INV" THEN 1040
970 GOSUB 2200
980 IF I=38 AND A=A(I) AND H(I)=1 THEN PRINT "I CAN'T, IT'S BOLTED TO THE FLOOR.": GOTO 560
990 IF I=76 AND A=A(I) AND H(I)=1 THEN PRINT "I CAN'T, THEY'RE FASTENED TO HIS FACE.": GOTO 560
1000 IF B=6 THEN PROCo("I'M CARRYING TOO MUCH. I'M NOT A FORKLIFT!"):PRINT: GOTO 560
1010 IF A(I)<>A OR H(I)=0 THEN 2230
1020 IF H(I)=1 THEN PRINT "I'M TOO WIMPY TO DO THAT.": GOTO 560 ELSE IF I=45 AND H(46)=0 THEN PRINT "I HEAR A JINGLE.": H(46)=2: A(I)=0: B=B+1: GOTO 560
1030 A(I)=0: B=B+1: PRINT "OK.": GOTO 560
1040 PRINT:PRINT "I AM CARRYING THE FOLLOWING:"
1050 IF B=0 THEN PRINT "NOTHING AT ALL.": GOTO 560
1060 FOR I=0 TO 83
1070 IF A(I)>0 THEN 1100
1090 PROCo(D$(I)+".  ")
1100 NEXT: PRINT : GOTO 560
1110 GOSUB 2200
1120 IF A(I)>0 THEN PRINT "I'M NOT CARRYING IT.": GOTO 560
1130 PRINT "IN ONE WORD TELL ME WHAT TO THROW IT AT."
1140 GOSUB 2000
1150 GOSUB 2200
1160 G=I: PRINT : INPUT "--> TELL ME WHAT TO DO? "E$
1170 GOSUB 2200
1180 IF A(I)<>A OR H(I)=0 THEN 2230 ELSE IF LEFT$(E$,3)="DOG" AND H(52)=1 THEN PRINT "DOG CONSUMES IT AND BURPS.": A(G)=A: H(G)=0: B=B-1: GOTO 560
1190 A(G)=A: B=B-1: PRINT "OK.": GOTO 560
1200 GOSUB 2200
1210 IF A(I)>0 THEN PRINT "I'M NOT CARRYING IT.": GOTO 560
1220 A(I)=A: B=B-1: PRINT "OK.": GOTO 560
1230 GOSUB 2200
1240 IF A(65)>0 THEN 2220
1250 IF I=65 THEN 2240
1260 IF I=81 THEN PRINT "I CAN'T, COWS ARE IMMUNE TO BB'S.": GOTO 560
1270 IF A<>A(I) THEN 2230
1280 PRINT "PLINK!"
1290 IF I=52 THEN D$(I)="DEAD GUARD DOG": H(I)=2
1300 IF I=57 THEN D$(I)="DEAD BUSDRIVER": H(I)=2
1310 GOTO 560
1320 GOSUB 2200: IF A<>A(I) OR H(I)=0 THEN 2230
1330 IF I=64 THEN PRINT "SQUEAK!": D$(42)="SPRINKLER": H(42)=2: H(43)=2 ELSE PRINT "NOTHING HAPPENS."
1340 GOTO 560
1350 IF A=35 AND (A$="" OR A$="DOW" OR A$="LED") THEN PRINT "SPLAT!": GOTO 2280 ELSE PRINT "I CAN'T GO IN THAT DIRECTION.": GOTO 560
1360 GOSUB 2200
1370 IF I=20 AND H(21)=0 THEN IF A=A(I) THEN PRINT "THERE'S A STRANGE NOISE.": H(21)=1: GOTO 560
1380 IF I=23 AND H(25)=0 THEN IF A=A(I) THEN PRINT "THERE'S A STRANGE NOISE.": H(25)=1: GOTO 560
1390 IF I=45 AND H(46)=0 THEN IF A=A(I) THEN PRINT "I HEAR A JINGLE.": H(46)=2: GOTO 560
1400 PRINT "OK.": GOTO 560
1410 GOSUB 2200: IF A<>A(I) AND A(I)<>0 OR H(I)=0 THEN 2230
1420 IF I=24 AND D$(24)="DRAWER" THEN H(22)=2: D$(24)="OPEN DRAWER": GOTO 1480
1430 IF I=48 AND D$(48)="MAILBOX" THEN H(53)=2: H(54)=2: H(55)=2: D$(48)="OPEN MAILBOX": GOTO 1480
1440 IF I<>74 OR A<>45 ELSE IF D$(74)="CLOSED DOOR" THEN H(75)=1: PRINT "I HEAR A DULL THUD.": D$(74)="OPEN DOOR WITH BRICK WALL BEYOND": GOTO 560 ELSE PRINT "IT'S ALREADY OPEN!": GOTO 560
1450 IF I=78 AND D$(78)="CHEST" THEN H(79)=2: D$(78)="OPEN CHEST": GOTO 1480
1460 IF I=37 AND NOT(D) THEN PRINT "OPENING AN UMBRELLA INSIDE IS BAD LUCK!": PROCo("A SECRET DOOR OPENS BELOW ME... I SLIDE DOWN A RAZOR BLADE INTO A POOL OF ALCOHOL."):PRINT: GOTO 2280
1470 PRINT "NOTHING HAPPENS.": GOTO 560
1480 PRINT "SOMETHING FALLS OUT.": GOTO 560
1490 GOSUB 2250: GOTO 560
1500 GOSUB 2200
1510 IF I<>7 THEN 2240
1520 FOR I=0 TO 83: IF A(I)=0 AND LEFT$(D$(I),1)="*" THEN A(I)=A: H(I)=0: B=B-1: PROCo("WHIRRR!  BUZZ SAW RIPS THE "+D$(I)+" RIGHT OUT MY HANDS AND DESTROYS IT!"):PRINT:i=I:I=83:NEXT:I=i: GOTO 560
1530 NEXT: PROCo("BUZZ SAW TURNS ON FOR A MOMENT... THEN STOPS."):PRINT: GOTO 560
1540 IF A$<>"LOC" THEN 2240
1550 IF A(55)<>0 OR A<>4 THEN 2220
1560 PRINT "OK.": D$(9)="UNLOCKED METAL DOOR": D$(10)="UNLOCKED METAL DOOR": GOTO 560
1570 GOSUB 2200: IF A(I)<>A AND A(I)<>0 OR H(I)=0 THEN 2230 ELSE IF I<>30 THEN PRINT "I CAN'T, IT'S UNBREAKABLE.": GOTO 560
1580 IF A(3)=0 AND (A(30)=0 OR A(30)=A) AND D$(30)="PIGGY BANK" THEN PRINT "SMASH!  I HEAR A JINGLE.": H(32)=2: A(32)=A: D$(30)="PIECES OF BROKEN PIGGY BANK": A$(30)="PIE" ELSE 2220
1590 GOTO 560
1600 GOSUB 2200: IF I<>32 THEN 2240
1610 IF A=39 AND A(32)=0 THEN PRINT "DOOR FLIES OPEN!": D$(62)="UNLOCKED WATER CLOSET": A(32)=39: H(32)=0: B=B-1 ELSE 2220
1620 GOTO 560
1630 GOSUB 2200: GOTO 2240
1640 GOSUB 2200: IF I<>38 THEN PRINT "IT ISN'T BOLTED.": GOTO 560
1650 IF A=31 AND A(5)=0 THEN PRINT "OK.": H(38)=2: H(39)=2: GOTO 560 ELSE 2220
1660 GOSUB 2200: IF A(I)<>A AND A(I)<>0 OR H(I)=0 THEN 2230
1670 IF I=73 THEN PRINT "BURP... BOY AM I PLASTERED!": GOTO 560 ELSE PRINT "YUCK!": GOTO 560
1680 GOSUB 2200: IF A(I)<>A AND A(I)<>0 OR H(I)=0 THEN 2230
1690 IF A(79)<>0 THEN PRINT "I DON'T HAVE A MATCH.": GOTO 560
1700 IF I=8 OR I=73 THEN PROCo("KABOOM!  MY BODY IS BLOWN INTO SMALL UNRECOGNIZABLE PIECES."):PRINT: B=B-2: A(I)=A: H(I)=0: A(79)=A: H(79)=0: GOTO 2280
1710 IF I=79 THEN PROCo("MATCH FLARES UP BRIEFLY... THEN DISINTEGRATES."):PRINT: B=B-1: A(79)=A: H(79)=0: GOTO 560
1720 PRINT "IT WON'T BURN.": GOTO 560
1730 GOSUB 2200: IF A(I)<>A OR H(I)=0 THEN 2230
1740 IF I=76 THEN PRINT "OK.": H(I)=2: A(I)=0: B=B+1: GOTO 560 ELSE PRINT "NOTHING HAPPENS.": GOTO 560
1750 GOSUB 2200: IF A(I)<>A AND A(I)<>0 OR H(I)=0 THEN 2230
1760 IF I=1 OR I=18 OR I=69 OR I=53 OR I=54 OR I=61 THEN PRINT "MAYBE I SHOULD READ IT?": GOTO 560
1770 IF I=2 OR I=29 OR I=72 OR I=9 OR I=10 OR I=47 OR I=82 OR I=11 OR I=25 OR I=21 OR I=26 OR I=35 OR I=62 OR I=31 THEN PROCo("THERE'S SOMETHING THERE... MAYBE I SHOULD GO THERE?"):PRINT: GOTO 560
1780 IF I=40 OR I=51 OR I=58 OR I=56 OR I=60 OR I=59 OR I=38 OR I=80 OR I=83 OR I=66 THEN PROCo("THERE'S SOMETHING THERE... MAYBE I SHOULD GO THERE?"):PRINT: GOTO 560
1790 IF I=75 AND H(77)=0 THEN H(76)=1: H(77)=2: GOTO 1860
1800 IF I=33 AND H(34)=0 THEN H(34)=2: GOTO 1860
1810 IF I=27 AND H(28)=0 THEN H(28)=2: GOTO 1860
1820 IF I=30 THEN PRINT "IT LOOKS BREAKABLE.": GOTO 560
1830 IF I=46 THEN PRINT "IT'S A KEY TO AN EDSEL.": GOTO 560
1840 IF I=42 AND H(43)=0 THEN 1860
1850 PRINT "I SEE NOTHING SPECIAL.": GOTO 560
1860 PRINT "THERE'S SOMETHING THERE.": GOTO 560
1870 GOSUB 2200: IF A(I)<>A AND A(I)<>0 OR H(I)=0 THEN 2230
1880 IF I=1 OR I=18 OR I=69 THEN PRINT "MAXIMUM CAPACITY: 1 PERSON OR 175 POUNDS": GOTO 560
1890 IF I=53 THEN PRINT "DEAR LORD,": PRINT "HAVING A GREAT TIME, WISH YOU WERE HERE.": GOTO 560
1900 IF I=54 THEN PRINT "NOTE FROM THE POST OFFICE:":PRINT"POSTAGE DUE: 42 CENTS.": GOTO 560
1910 IF I=61 THEN PROCo("YOUR MISSION IS TO REPOSSESS THE 13 *TREASURES* OF LORD HOG JOWL AND STORE THEM IN A PREDETERMINED PLACE... GOOD LUCK!"):PRINT:GOTO 560
1920 PRINT "I CAN'T, IT'S ILLEGIBLE.": GOTO 560
1930 IF A<25 ON A GOTO 1950,1950,1960,1960,1950,1950,1950,1950,1950,1950,1950,1950,1950,1950,1950,1950,1950,1950,1950,1950,1950,1950,1970,1970
1940 ON A-24 GOTO 1950,1950,1950,1950,1950,1950,1950,1950,1950,1950,1950,1980,1950,1980,1990,1950,1950,1950,1950,1950,1950,1950,1980,1950
1950 PRINT "TRY EXAMINING THINGS.": GOTO 560
1960 PRINT "I'M PRETTY GOOD AT PICKING LOCKS.": GOTO 560
1970 PRINT "I USED TO BE A -MOVING- MAN.": GOTO 560
1980 PRINT "I'M A PRETTY GOOD MARKSMAN.": GOTO 560
1990 PRINT "IT'S A PAY TOILET.": GOTO 560
2000 VDU30:REPEAT PRINT SPC40;:UNTIL VPOS>V%+1:VDU30
2010 PRINT'"I AM ";: IF LEFT$(C$(A),1)="*" THEN PROCo(RIGHT$(C$(A),LEN(C$(A))-1)): D=-1 ELSE PROCo(C$(A)): D=0
2020 FOR I=0 TO 83: IF A(I)=A AND H(I)>0 THEN i=I:I=83:NEXT:I=i:GOTO 2030 ELSE NEXT: GOTO 2090
2030 PRINT:PROCb:PRINT"VISIBLE ITEMS: ";
2040 FOR I=0 TO 83
2050 IF A(I)<>A OR H(I)=0 THEN 2080
2060 REM
2070 PROCo(D$(I)+". ")
2080 NEXT
2090 PRINT
2100 F$=""
2110 IF B(A)>0 THEN F$="NORTH "
2120 IF C(A)>0 THEN F$=F$ + "SOUTH "
2130 IF D(A)>0 THEN F$=F$ + "EAST "
2140 IF E(A)>0 THEN F$=F$ + "WEST "
2150 IF F(A)>0 THEN F$=F$ + "UP "
2160 IF G(A)>0 THEN F$=F$ + "DOWN"
2170 IF F$="" THEN 2190
2180 PROCb: PROCo("SOME OBVIOUS EXITS ARE: "+F$):PRINT
2190 PRINT C$;:V%=VPOS:RETURN
2200 FOR I=0 TO 83: IF A$(I)=LEFT$(E$,3) THEN H=I: IF A(I)=A OR A(I)=0 THEN i=I:I=83:NEXT:I=i: RETURN
2210 NEXT: I=H: IF A$(I)=LEFT$(E$,3) THEN RETURN ELSE PRINT "I DON'T KNOW WHAT " CHR$(34)E$ CHR$(34)" IS.": GOTO 560
2220 PRINT "I CAN'T DO THAT... YET!": GOTO 560
2230 PRINT "I DON'T SEE IT HERE.": GOTO 560
2240 PROCo("I MUST BE STUPID, BUT I DON'T UNDERSTAND WHAT YOU MEAN."):PRINT: GOTO 560
2250 E=0: FOR I=0 TO 83: IF A(I)=40 AND LEFT$(D$(I),1)="*" THEN E=E+1
2260 NEXT: F=E*100/13: PROCo("YOU STORED "+STR$(E)+" TREASURES. ON A SCALE OF 0 TO 100 THAT RATES "+STR$(F)+". ")
2270 IF E=13 THEN PROCo("EXCELLENT! YOU HAVE COMPLETED THIS ADVENTURE."):PRINT: GOTO 2310 ELSE PRINT: RETURN
2280 PRINT "I'M DEAD.": PRINT "MY BODY TELEPORTS ELSEWHERE."
2290 PROCo("I SINCERELY HOPE I CAN FIND MY WAY OUT OF HERE.  I DON'T THINK I CAN STAND IT MUCH LONGER!"):PRINT: A=41: GOTO 560
2300 GOSUB 2250
2310 PRINT "THIS ADVENTURE IS OVER."
2320 GOSUB 2000: PRINTTAB(0,24);:INPUT "DO YOU WANT TO PLAY THIS ADVENTURE AGAIN";G$
2330 IF LEFT$(G$,1)="Y" THEN GOSUB 2000: RUN ELSE IF LEFT$(G$,1)="N" THEN CLS: END ELSE 2320

REM Word-wrap
2400 DEFPROCo($S%):LOCALA%,Z%,C%,N%:N%=LEN$S%:A%=0:Z%=41-POS:REPEATIFZ%>N%Z%=N%ELSEREPEATZ%=Z%-1:C%=S%?Z%:UNTILC%=32:IF Z%>=A% ELSE IF POS PRINT:Z%=41:UNTIL0 ELSE STOP
2410 S%?Z%=&D:PRINT$(S%+A%);:IFZ%<N% IF POS PRINT
2420 A%=Z%+1:Z%=A%+41:UNTILA%>=N%:ENDPROC

REM Is the previous line blank?
2430 DEFFNb:LOCALA%,C%,I%:VDU11:IF HIMEM<&7C01 ELSE VDU8:A%=&87:FORI%=1TO40:VDU9:C%=(USR(&FFF4)AND&FF00)DIV256:IFC%=32NEXT:VDU10,13:=TRUE ELSEI%=40:NEXT:VDU10,13:=FALSE
2440 A%=?&34B*256+?&34A:VDU 10:FOR I%=0 TO 39 STEP 4:C%=A%+I%:C%=C%+&400*(C%>&7FFF):IF !C%=&20202020 NEXT:=TRUE ELSE I%=39:NEXT:=FALSE

REM If at start of line (but not at 0,0) then print a blank line if prev line isn't blank
2450 DEFPROCb:IFPOS ENDPROC ELSE IF VPOS=0 ENDPROC ELSE IF FNb ENDPROC ELSE PRINT:ENDPROC


